# Maintainer: Armin K. <krejzi at email dot com>
# Contributor: Kristian Klausen <klausenbusk@hotmail.com>
# Contributor: Egon Ashrafinia <e.ashrafinia@gmail.com>
# Contributor: Tavian Barnes <tavianator@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Antti "Tera" Oja <antti.bofh@gmail.com>
# Contributor: Diego Jose <diegoxter1006@gmail.com>

pkgbase=mesa-git
pkgname=('opencl-mesa-git' 'mesa-dri-git' 'mesa-git' 'mesa-libgl-git')
pkgver=10.4.0_devel.65028
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('python2' 'libxml2' 'libx11' 'glproto' 'libdrm' 'dri2proto' 'dri3proto' 'presentproto' 
             'libxshmfence' 'libxxf86vm'  'libxdamage' 'libvdpau' 'libxvmc' 'wayland' 'elfutils'
             'llvm' 'systemd' 'libomxil-bellagio' 'libclc' 'clang' 'git')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=('mesa::git://anongit.freedesktop.org/mesa/mesa#branch=master'
        'LICENSE')
md5sums=('SKIP'
         '5c65a0fe315dd347e09b1f2826a1df5a')

pkgver() {
    cd "${srcdir}/mesa"
    echo $(cat VERSION | tr "-" "_").$(git rev-list --count HEAD)
}

_mesaver() {
    path="${srcdir}/mesa/VERSION"
    [ -f $path ] && cat "$path"
}

build () {
  cd "${srcdir}/mesa"

  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --with-dri-driverdir=/usr/lib/xorg/modules/dri \
               --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
               --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
               --with-egl-platforms=x11,drm,wayland \
               --enable-llvm-shared-libs \
               --disable-gallium-egl \
               --disable-gallium-gbm \
               --enable-egl \
               --enable-gbm \
               --enable-gallium-llvm \
               --enable-shared-glapi \
               --enable-glx-tls \
               --enable-dri \
               --enable-glx \
               --enable-osmesa \
               --enable-gles1 \
               --enable-gles2 \
               --enable-texture-float \
               --enable-xa \
               --enable-vdpau \
               --enable-xvmc \
               --enable-dri3 \
               --enable-omx \
               --enable-opencl \
               --enable-opencl-icd \
               --with-clang-libdir=/usr/lib

  make

  # fake installation
  mkdir -p "${srcdir}/fakeinstall"
  make DESTDIR="${srcdir}/fakeinstall" install
}

package_opencl-mesa-git () {
  pkgdesc="OpenCL support for AMD/ATI Radeon Mesa drivers"
  depends=('mesa-libgl' "mesa=$(_mesaver)" 'libcl' 'libclc' 'clang')
  optdepends=('opencl-headers: headers necessary for OpenCL development')
  provides=('opencl-mesa')
  replaces=('opencl-mesa')
  conflicts=('opencl-mesa')

  install -v -m755 -d "${pkgdir}/etc"
  mv -v "${srcdir}/fakeinstall/etc/OpenCL" "${pkgdir}/etc/"
 
  install -v -m755 -d "${pkgdir}/usr/lib"
  mv -v "${srcdir}"/fakeinstall/usr/lib/lib*OpenCL* "${pkgdir}/usr/lib/"
  mv -v "${srcdir}/fakeinstall/usr/lib/gallium-pipe" "${pkgdir}/usr/lib/"

  install -v -m755 -d "${pkgdir}/usr/share/licenses/opencl-mesa"
  install -v -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/opencl-mesa/"

  ln -sv opencl-mesa "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_mesa-dri-git() {
  pkgdesc="Mesa DRI drivers for AMD, Intel, NVIDIA and VMWare"
  depends=('mesa-libgl' "mesa=$(_mesaver)" 'libomxil-bellagio' 'libvdpau' 'libxvmc' 'libtxc_dxtn')
  provides=('ati-dri' 'intel-dri' 'nouveau-dri' 'svga-dri' 'mesa-dri' 'ati-dri-git' 'intel-dri-git' 'nouveau-dri-git' 'svga-dri-git')
  replaces=('ati-dri' 'intel-dri' 'nouveau-dri' 'svga-dri' 'mesa-dri' 'ati-dri-git' 'intel-dri-git' 'nouveau-dri-git' 'svga-dri-git')
  conflicts=('ati-dri' 'intel-dri' 'nouveau-dri' 'svga-dri' 'mesa-dri' 'ati-dri-git' 'intel-dri-git' 'nouveau-dri-git' 'svga-dri-git')

  install -v -m755 -d "${pkgdir}/etc"
  mv -v "${srcdir}/fakeinstall/etc/drirc" "${pkgdir}/etc/"

  install -v -m755 -d "${pkgdir}/usr/lib/xorg/modules/dri"

  mv -v "${srcdir}/fakeinstall/usr/lib/bellagio" "${pkgdir}/usr/lib/"
  mv -v "${srcdir}/fakeinstall/usr/lib/vdpau" "${pkgdir}/usr/lib/"

  mv -v "${srcdir}"/fakeinstall/usr/lib/libXvMC{nouveau,r600}.so* "${pkgdir}/usr/lib/"

  # ati-dri
  mv -v "${srcdir}"/fakeinstall/usr/lib/xorg/modules/dri/{r200,r300,r600,radeon,radeonsi}_dri.so "${pkgdir}/usr/lib/xorg/modules/dri/"
  # intel-dri
  mv -v "${srcdir}/fakeinstall/usr/lib/xorg/modules/dri"/{i915,i965}_dri.so "${pkgdir}/usr/lib/xorg/modules/dri/"
  # nouveau-dri
  mv -v "${srcdir}/fakeinstall/usr/lib/xorg/modules/dri"/nouveau_{dri,vieux_dri}.so "${pkgdir}/usr/lib/xorg/modules/dri/"
  # svga-dri
  mv -v "${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/vmwgfx_dri.so" "${pkgdir}/usr/lib/xorg/modules/dri/"

  install -v -m755 -d "${pkgdir}/usr/share/licenses/mesa-dri"
  install -v -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-dri/"

  ln -sv mesa-dri "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_mesa-git () {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libvdpau' 'wayland' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'systemd' 'elfutils' 'llvm-libs' 'libomxil-bellagio')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=("mesa=$(_mesaver)" 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  replaces=('mesa' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  conflicts=('mesa' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')

  mv -v "${srcdir}/fakeinstall/usr" "${pkgdir}/"
 
  install -v -m755 -d "${pkgdir}/usr/lib/mesa"

  # move libgl/EGL/glesv*.so to not conflict with blobs - may break .pc files ?
  mv -v "${pkgdir}"/usr/lib/libGL.so* 		"${pkgdir}/usr/lib/mesa/"
  mv -v "${pkgdir}"/usr/lib/libEGL.so* 		"${pkgdir}/usr/lib/mesa/"
  mv -v "${pkgdir}"/usr/lib/libGLES*.so*	"${pkgdir}/usr/lib/mesa/"

  install -v -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -v -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"

  ln -sv mesa "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_mesa-libgl-git () {
  pkgdesc="Mesa 3-D graphics library"
  depends=("mesa=$(_mesaver)")
  provides=("mesa-libgl=$(_mesaver)" "libgl=$(_mesaver)")
  replaces=('mesa-libgl')
  conflicts=('mesa-libgl')

  # See FS#26284
  install -v -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -sv libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -sv /usr/lib/mesa/libGL.so.1.2.0	"${pkgdir}/usr/lib/libGL.so.1.2.0"
  ln -sv libGL.so.1.2.0			"${pkgdir}/usr/lib/libGL.so.1"
  ln -sv libGL.so.1.2.0			"${pkgdir}/usr/lib/libGL.so"

  ln -sv /usr/lib/mesa/libEGL.so.1.0.0	"${pkgdir}/usr/lib/libEGL.so.1.0.0"
  ln -sv libEGL.so.1.0.0		"${pkgdir}/usr/lib/libEGL.so.1"
  ln -sv libEGL.so.1.0.0		"${pkgdir}/usr/lib/libEGL.so"

  ln -sv /usr/lib/mesa/libGLESv1_CM.so.1.1.0	"${pkgdir}/usr/lib/libGLESv1_CM.so.1.1.0"
  ln -sv libGLESv1_CM.so.1.1.0			"${pkgdir}/usr/lib/libGLESv1_CM.so.1"
  ln -sv libGLESv1_CM.so.1.1.0			"${pkgdir}/usr/lib/libGLESv1_CM.so"

  ln -sv /usr/lib/mesa/libGLESv2.so.2.0.0	"${pkgdir}/usr/lib/libGLESv2.so.2.0.0"
  ln -sv libGLESv2.so.2.0.0			"${pkgdir}/usr/lib/libGLESv2.so.2"
  ln -sv libGLESv2.so.2.0.0			"${pkgdir}/usr/lib/libGLESv2.so"

  install -v -m755 -d "${pkgdir}/usr/share/licenses/mesa-libgl"
  install -v -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-libgl/"

  ln -sv mesa-libgl "${pkgdir}/usr/share/licenses/${pkgname}"
}
